
void: make_packet(uint8*: p)
{
    push32(p, 10); // same as *(uint32*)p = 10; p += sizeof(uint32);
    push32(p, 255);
    push16(p, 0xFFFF);
    push16(p, 0);
    push8(p, 'a');
    push8(p, 'z');
}

"hello world!\n";

uint8[16]: packet;
make_packet(packet);

[ "this is the packet (%d bytes): %*X", lenof(packet), packet ];

// hello world!
// [test.lc:17] this is the packet (16 bytes): 0A 00 00 00 FF 00 00 00 FF
// FF 00 00 00 00 61 7A

// ------------------------------------------------------------------------

uint64: varg_test(...)
{
    for (uint64: i = 0; i < argc; ++i) {
        "%d: %s ", i, argv[i];
    }

    '\n';

    return argc;
}

uint64: n = varg_test("hello", "this", "is", "a", "test");
"%d args passed\n", n;

// hello this is a test
// 5 args passed

// ------------------------------------------------------------------------

// switch with no implicit fallthrough. if you don't want to use switch
// and still have fallthrough you can explicitly add "fallthrough;"
// anywhere in the case you want that to happen

void: describe_char(int8: c)
{
    pick (c)
    {
    case 'a' ... 'z':
    case 'A' ... 'Z':
        "letter";
    case '0' ... '9':
        "number";
    default:
        "other";
    }

    "\n";
}

// ------------------------------------------------------------------------

// like in HolyC, you can have implicit cases that are 1 + the previous
// case

for (int64: i = 0; i < 10; ++i)
{
    pick (i)
    {
    case: "one";
    case: "two";
    case: "three";
    case 10: "ten";
    case: "eleven";
    }
}

// ------------------------------------------------------------------------

double: x = 5.5;
double: y = 10.123123;

"pixel position: %d %d\n", int(x), int(y);

// there's also double() to convert to double

// ------------------------------------------------------------------------

// there is no preprocessor or const, if you need to define constants use
// enum

enum
{
    TEN = 10,
    ELEVEN,
    TWELVE,
    THIRTEEN,
};

enum PI = 3.14159265358979323846;

// note that enum is always anonymous, unlike C and C++

// ------------------------------------------------------------------------

// structs behave mostly like C except that you don't need the struct
// prefix

struct point
{
    double: x, y;
};

point: pt;
pt.x = 10;
pt.y = 20;

pt = { 10, 20 };
pt = { .x = 10, .y = 20 };
pt = { .y = 20, .x = 10 };

// automatically picks next field if unspecified
pt = { .x = 10, 20 };

// omitted fields are initialized to zero

// there is no -> operator, members of struct pointers are also accessed
// with '.'

point*: ppt = &pt;
ppt.x = 50;
ppt.y = 70;

// the dump builtin generates printf calls for all struct members
// recursively

dump(pt);

// point: pt = {
//     /* double */ .x = 50.0000,
//     /* double */ .y = 70.0000,
// };
//

// it can also take multiple values

int: a = 50;
double: b = 123.45;
dump(pt, ppt, a, b, 10, 20);

// point: pt = {
//     /* double */ .x = 50.0000,
//     /* double */ .y = 70.0000,
// };
// point*: ppt = 0xBADDCAFEBAADF00D;
// *ppt = {
//     /* double */ .x = 50.0000,
//     /* double */ .y = 70.0000,
// };
// int: a = 50;
// int: b = 123.45;
// int(10);
// int(20);

// ------------------------------------------------------------------------

// non-pointer and non-array types can still be indexed and they will be
// automagically used as uint8 pointers

uint32: a = 0x01020304;
uint64: p = &a;

dump(p[0], p[1], p[2], p[3]);

// /* uint8 */ p[0] = 4;
// /* uint8 */ p[1] = 3;
// /* uint8 */ p[2] = 2;
// /* uint8 */ p[3] = 1;

// ------------------------------------------------------------------------

// function pointers are stored in void*, there are no function types.
// parameter count and types are not checked, it's up to you to not
// do anything wrong

void: mycallback(int64: a, int64: b)
{
    "%d", (a * b) / 2;
}

void*: p = mycallback;

void: callit(void*: callback)
{
    callback(10, 20);
}

callit(mycallback);
